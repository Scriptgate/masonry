apply plugin: 'groovy'
apply plugin: 'maven'

group 'net.scriptgate.masonry'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://github.com/Scriptgate/scriptgate-repo/raw/master"
    }
}

sourceSets {
    api
}

configurations {
    compile.extendsFrom apiCompile
}

configurations.all {
    it.resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

dependencies {

    apiCompile localGroovy()
    apiCompile group: 'net.scriptgate.engine', name: 'engine-api', version: '0.0.1-SNAPSHOT', changing: true

    compile(
            sourceSets.api.output,
            'com.google.guava:guava:19.0',
            'org.slf4j:slf4j-api:1.7.13'
    )

    testCompile group: 'net.scriptgate.engine', name: 'engine-lwjgl', version: '0.0.1-SNAPSHOT', changing: true

    testCompile(
            'org.slf4j:slf4j-simple:1.7.13',
            'junit:junit:4.12',
            'org.assertj:assertj-core:3.3.0'
    )
}



task createPom << {
    pom {
        project {
            groupId project.group
            artifactId project.name
            version project.version
            description 'POM was created with Gradle'
        }
    }.writeTo("${project.libsDir}/${project.name}-${project.version}.pom")
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
}